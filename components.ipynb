{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Distributions\n",
    "using Plots\n",
    "\n",
    "abstract type Component end\n",
    "abstract type Scatterer end\n",
    "abstract type Model end\n",
    "\n",
    "function flattenall(A)\n",
    "    V = []\n",
    "    for x in A\n",
    "        if isa(x, AbstractArray)\n",
    "            append!(V, flattenall(x))\n",
    "        else\n",
    "            push!(V, x)\n",
    "        end\n",
    "    end\n",
    "    V\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# data stuff\n",
    "using DelimitedFiles\n",
    "\n",
    "\n",
    "mutable struct Data1D\n",
    "    x::Vector\n",
    "    y::Vector\n",
    "    yerr::Union{Vector, Nothing}\n",
    "    xerr::Union{Vector, Nothing}\n",
    "    fname::Union{String, Nothing}\n",
    "    Data1D(x::Vector, y::Vector) = new(x, y, nothing, nothing, nothing)\n",
    "    Data1D(x::Vector, y::Vector, yerr::Vector) = new(x, y, yerr, nothing)\n",
    "    Data1D(x::Vector, y::Vector, yerr::Vector, xerr::Vector) = new(x, y, yerr, xerr)\n",
    "    Data1D(x::Vector, y::Vector; yerr::Vector, xerr::Vector) = new(x, y, yerr, xerr)\n",
    "    function Data1D(x::Vector, y::Vector;\n",
    "            yerr::Vector,\n",
    "            xerr::Vector\n",
    "        )\n",
    "        new(x, y, yerr, xerr)\n",
    "    end\n",
    "    Data1D(filename::String) = read_data(filename)\n",
    "end\n",
    "\n",
    "\n",
    "function read_data(filename::String; delim=nothing)\n",
    "    if isnothing(delim)\n",
    "        arr = readdlm(filename)\n",
    "    else\n",
    "        arr = readdlm(filename, delim)\n",
    "    end\n",
    "    nrows, ncols = size(arr)\n",
    "\n",
    "    if ncols == 2\n",
    "        data = Data1D(arr[:, 1], arr[:, 2], nothing, nothing)\n",
    "    elseif ncols == 3\n",
    "        data = Data1D(arr[:, 1], arr[:, 2], arr[:, 3], nothing)\n",
    "    elseif ncols == 4\n",
    "        data = Data1D(arr[:, 1], arr[:, 2], arr[:, 3], arr[:, 4])\n",
    "    end\n",
    "    data.fname = filename\n",
    "    return data\n",
    "end\n",
    "\n",
    "\n",
    "function refresh!(data::Data1D)\n",
    "   data_updated = read_data(data.fname)\n",
    "   data.x = data_updated.x\n",
    "   data.y = data_updated.y\n",
    "   data.yerr = data_updated.yerr\n",
    "   data.xerr = data_updated.xerr\n",
    "   return nothing\n",
    "end\n",
    "\n",
    "\n",
    "function Base.size(data::Data1D)\n",
    "    return size(data.x)\n",
    "end\n",
    "\n",
    "function Base.length(data::Data1D)\n",
    "    return length(data.x)\n",
    "end\n",
    "\n",
    "Plots.plot(data::Data1D) = plot(data.x, data.y, yaxis=:log)\n",
    "Plots.plot!(data::Data1D) = plot!(data.x, data.y, yaxis=:log)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# parameters\n",
    "\n",
    "mutable struct Parameter\n",
    "    value::Real\n",
    "    vary::Bool\n",
    "    bounds::Distribution\n",
    "    name::Union{String, Symbol}\n",
    "    function Parameter(value::Real;\n",
    "            vary::Bool=false,\n",
    "            bounds::Distribution=Uniform(-Inf, Inf),\n",
    "            name::Union{String, Symbol}=\"\"\n",
    "        )\n",
    "        new(value, vary, bounds, name)\n",
    "    end\n",
    "end\n",
    "\n",
    "function build_parameter(p::Union{Parameter, Real}; name::Union{String, Symbol}=\"\", vary=false)\n",
    "    isa(p, Parameter) ? p : Parameter(p; name=name, vary=vary)\n",
    "end\n",
    "\n",
    "# might need to get rid of this when it comes to autodifferentiation!\n",
    "Base.Real(p::Parameter) = p.value"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "const TINY = 1e-30\n",
    "const _FWHM = 2 * sqrt(2 * log(2.0))\n",
    "const PI4 = 4e-6 * pi\n",
    "const _INTLIMIT = 3.5\n",
    "\n",
    "function abeles(q, w)\n",
    "    nlayers = size(w, 1) - 2\n",
    "    npnts = length(q)\n",
    "\n",
    "    reflectivity = Vector{Any}(undef, (npnts))\n",
    "    oneC = Complex(1.0)\n",
    "\n",
    "    for j in eachindex(q)\n",
    "        qq2 = (q[j] * q[j] / 4.0) + 0.0im\n",
    "        kn = (q[j] / 2.) + 0.0im\n",
    "\n",
    "        # variables are local to if blocks\n",
    "        local MRtotal11, MRtotal12, MRtotal21, MRtotal22\n",
    "\n",
    "        for i = 1:nlayers+1\n",
    "            # wavevector in the layer\n",
    "            sld_next = ((w[i+1, 2] - w[1, 2]) + ((abs(w[i+1, 3]) + TINY))im) * pi * 4.0e-6\n",
    "            kn_next = sqrt(qq2 - sld_next)\n",
    "\n",
    "            # reflectance of the interface\n",
    "            rj = (kn - kn_next)/(kn + kn_next) * exp(kn * kn_next * (-2.0 * w[i+1, 4]^2))\n",
    "\n",
    "            if i == 1\n",
    "                # characteristic matrix for first interface\n",
    "                MRtotal11 = oneC\n",
    "                MRtotal12 = rj\n",
    "                MRtotal21 = rj\n",
    "                MRtotal22 = oneC\n",
    "            else\n",
    "                # work out the beta for the layer\n",
    "                beta = exp(kn * (abs(w[i, 1]) * 1im))\n",
    "\n",
    "                # this is the characteristic matrix of a layer\n",
    "                MI11 = beta\n",
    "                MI12 = rj * beta\n",
    "                MI22 = oneC / beta\n",
    "                MI21 = rj * MI22\n",
    "\n",
    "                # propagate optical matrix by matmul\n",
    "                p11 = MRtotal11 * MI11 + MRtotal12 * MI21\n",
    "                p12 = MRtotal11 * MI12 + MRtotal12 * MI22\n",
    "                p21 = MRtotal21 * MI11 + MRtotal22 * MI21\n",
    "                p22 = MRtotal21 * MI12 + MRtotal22 * MI22\n",
    "\n",
    "                MRtotal11 = p11\n",
    "                MRtotal12 = p12\n",
    "                MRtotal21 = p21\n",
    "                MRtotal22 = p22\n",
    "\n",
    "            end\n",
    "            kn = kn_next;\n",
    "\n",
    "        end\n",
    "        reflectivity[j] = MRtotal21 / MRtotal11\n",
    "    end\n",
    "    return real(reflectivity .* conj(reflectivity))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Structure and reflectometry stuff\n",
    "\n",
    "struct Structure <: AbstractVector{Component}\n",
    "    components::Vector{Component}\n",
    "end\n",
    "Structure() = Structure([])\n",
    "slabs(structure::Structure) = hcat([slabs(c) for c in structure.components]...)'\n",
    "parameters(structure::Structure) = [parameters(c) for c in structure.components]\n",
    "\n",
    "Base.length(s::Structure) = length(s.components)\n",
    "Base.size(s::Structure) = size(s.components)\n",
    "Base.getindex(s::Structure, i::Int) = s.components[i]\n",
    "Base.IndexStyle(::Type{<:Structure}) = IndexLinear()\n",
    "function Base.push!(s::Structure, items...)\n",
    "    for item in items\n",
    "        push!(s.components, item)\n",
    "    end\n",
    "end\n",
    "function Base.append!(s::Structure, arr::Array{N, 1} where N<:Component)\n",
    "    for item in arr\n",
    "        push!(s.components, item)\n",
    "    end\n",
    "end\n",
    "Base.setindex!(s::Structure, c::Component, i::Int) = setindex!(s.components, c, i)\n",
    "\n",
    "\n",
    "(|)(a::Component, b::Component) = Structure([a, b])\n",
    "function |(s::Structure, b::Component)\n",
    "    push!(s, b)\n",
    "    s\n",
    "end\n",
    "function |(s::Structure, arr::Array{N, 1} where N<:Component)\n",
    "    append!(s, arr)\n",
    "    s\n",
    "end\n",
    "\n",
    "\n",
    "mutable struct SLD <: Scatterer\n",
    "    re::Parameter\n",
    "    im::Parameter\n",
    "    SLD(re::Union{Real, Parameter}) = new(build_parameter(re), Parameter(0.0))\n",
    "    SLD(re::Union{Real, Parameter}, im::Union{Real, Parameter}) = new(\n",
    "        build_parameter(re), build_parameter(im)\n",
    "    )\n",
    "    SLD(sld::Complex) = new(Parameter(sld.re), Parameter(sld.im))\n",
    "end\n",
    "sld(s::SLD) = s.re.value + 1.0im * s.im.value\n",
    "Base.complex(s::SLD) = s.re.value + 1.0im * s.im.value\n",
    "parameters(s::SLD) = [s.re, s.im]\n",
    "\n",
    "\n",
    "mutable struct Slab <: Component\n",
    "    thickness::Parameter\n",
    "    scatterer::Scatterer\n",
    "    roughness::Parameter\n",
    "    vfsolv::Parameter\n",
    "    function Slab(thickness::Union{Parameter, Real},\n",
    "        scatterer::Union{Scatterer, Real, Complex, Tuple{Real, Real}},\n",
    "        roughness::Union{Parameter, Real},\n",
    "        vfsolv::Union{Parameter, Real})\n",
    "        if isa(scatterer, Scatterer)\n",
    "            s = scatterer\n",
    "        else\n",
    "            s = SLD(scatterer)\n",
    "        end\n",
    "        new(build_parameter(thickness), s, build_parameter(roughness), build_parameter(vfsolv)\n",
    "    )\n",
    "    end\n",
    "end\n",
    "\n",
    "function slabs(slab::Slab)\n",
    "    sldv = sld(slab.scatterer)\n",
    "    return [slab.thickness.value, sldv.re, sldv.im, slab.roughness.value]\n",
    "end\n",
    "parameters(slab::Slab) = [slab.thickness, parameters(slab.scatterer), slab.roughness, slab.vfsolv]\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "mutable struct ReflectModel <: Model\n",
    "    structure::Structure\n",
    "    scale::Parameter\n",
    "    background::Parameter\n",
    "    dq::Parameter\n",
    "    \n",
    "    function ReflectModel(s::Structure;\n",
    "            scale::Union{Real, Parameter}=1.0,\n",
    "            background::Union{Real, Parameter}=0.0,\n",
    "            dq::Union{Real, Parameter}=5.0\n",
    "        )\n",
    "        new(\n",
    "            s,\n",
    "            build_parameter(scale, name=\"scale\"),\n",
    "            build_parameter(background, name=\"background\"),\n",
    "            build_parameter(dq, name=\"dq\")\n",
    "        )\n",
    "    end\n",
    "end\n",
    "\n",
    "generative(model::Model, x::Vector) = nothing\n",
    "function generative(model::ReflectModel, x::Vector; xerr=nothing)\n",
    "    w = slabs(model.structure)\n",
    "    model.scale.value .* abeles(x, w) .+ model.background.value\n",
    "end\n",
    "\n",
    "parameters(model::ReflectModel) = [model.scale, model.background, model.dq, parameters(model.structure)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "mutable struct Objective\n",
    "    model::Model\n",
    "    data::Data1D\n",
    "end\n",
    "\n",
    "function logl(objective::Objective)\n",
    "    gen = generative(objective.model, objective.data.x; xerr=objective.data.xerr)\n",
    "\n",
    "    sd = objective.data.yerr\n",
    "    if isnothing(objective.data.yerr)\n",
    "        sd = fill(1.0, size(objective.data.y))\n",
    "    end\n",
    "    var_y = sd.^2.0\n",
    "\n",
    "    logl = log.(2 .* pi .* var_y)\n",
    "    logl .+= (objective.data.y .- gen).^2.0 ./ var_y\n",
    "    return -0.5 * sum(logl)\n",
    "end\n",
    "parameters(obj::Objective) = flattenall(parameters(obj.model))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "data = Data1D(\"c_PLP0011859_q.txt\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "air = SLD(0.0)\n",
    "sio2 = SLD(3.47)\n",
    "polymer = SLD(2.74)\n",
    "si = SLD(2.07)\n",
    "d2o = SLD(6.36)\n",
    "\n",
    "air_l = Slab(0, air, 0, 0)\n",
    "sio2_l = Slab(39.724, sio2, 3, 0)\n",
    "si_l = Slab(0, si, 0, 0)\n",
    "polymer_l = Slab(259.433, polymer, 3, 0)\n",
    "d2o_l = Slab(0, d2o, 3, 0)\n",
    "\n",
    "s = si_l | sio2_l | polymer_l | d2o_l\n",
    "\n",
    "model = ReflectModel(s; background=3.32e-7);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "objective = Objective(model, data)\n",
    "logl(objective)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "x = collect(range(0.0051, 0.5; length=201))\n",
    "y = generative(model, x)\n",
    "plot(x, y, yaxis=:log)\n",
    "plot!(data)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "parameters(objective)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.3",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
